Name "linuxtrack-wine"
OutFile "linuxtrack-wine.exe"
InstallDir $PROGRAMFILES\Linuxtrack
InstallDirRegKey HKLM "Software\Linuxtrack" "InstallDir"

Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

Section "Linuxtrack / TrackIR Interface"
  SectionIn RO
  SetOutPath $INSTDIR
  File /oname=Controller.exe controller/Controller.exe.so
  File /oname=Tester.exe tester\Tester.exe.so
  File /oname=NPClient.dll client\NPClient.dll.so
  File /oname=check_data.exe client\check_data.exe.so
  File /oname=linuxtrack.ico @srcdir@\controller\linuxtrack.ico
  File /oname=TrackIR.exe views\TrackIR.exe.so
  WriteRegStr HKLM SOFTWARE\Linuxtrack "Install_dir" "$INSTDIR"
  WriteRegStr HKCU "Software\NaturalPoint\NATURALPOINT\NPClient Location" "Path" "$INSTDIR\"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Linuxtrack" "DisplayName" "Linuxtrack wine bridge"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Linuxtrack" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Linuxtrack" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Linuxtrack" "NoRepair" 1
  WriteUninstaller "uninstall.exe"
  Exec '"$INSTDIR\check_data.exe"'
SectionEnd

Section "Linuxtrack / Freetrack Interface"
  SetOutPath $INSTDIR
  File /oname=FreeTrackTester.exe ft_tester\ftc.exe.so
  File /oname=FreeTrackClient.dll ft_client\FreeTrackClient.dll.so
  WriteRegStr HKCU "Software\Freetrack\FreetrackClient" "Path" "$INSTDIR\"
SectionEnd

Section "Start Menu Shortcuts"
  CreateDirectory "$SMPROGRAMS\Linuxtrack"
  CreateShortCut "$SMPROGRAMS\Linuxtrack\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\Linuxtrack\Controller.lnk" "$INSTDIR\Controller.exe" "" "$INSTDIR\linuxtrack.ico" 0
  CreateShortCut "$SMPROGRAMS\Linuxtrack\TrackIR.lnk" "$INSTDIR\TrackIR.exe" "" "$INSTDIR\linuxtrack.ico" 0
  CreateShortCut "$SMPROGRAMS\Linuxtrack\Tester.lnk" "$INSTDIR\Tester.exe" "" "$INSTDIR\Tester.exe"
  CreateShortCut "$SMPROGRAMS\Linuxtrack\FreeTrackTester.lnk" "$INSTDIR\FreeTrackTester.exe" "" "$INSTDIR\FreeTrackTester.exe"
SectionEnd

Section "Uninstall"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Linuxtrack"
  DeleteRegKey HKLM SOFTWARE\Linuxtrack
  DeleteRegKey HKCU Software\NaturalPoint
  DeleteRegKey HKCU Software\Freetrack
  Delete $INSTDIR\Controller.exe
  Delete $INSTDIR\Tester.exe
  Delete $INSTDIR\NPClient.dll
  Delete $INSTDIR\FreeTrackClient.dll
  Delete $INSTDIR\FreeTrackTester.exe
  Delete $INSTDIR\uninstall.exe
  Delete $INSTDIR\check_data.exe
  Delete $INSTDIR\TrackIR.exe
  Delete $INSTDIR\linuxtrack.ico
  Delete $INSTDIR\TIRViews.dll
  Delete $INSTDIR\mfc42u.dll
  Delete "$SMPROGRAMS\Linuxtrack\*.*"
  RMDir "$SMPROGRAMS\Linuxtrack"
  RMDir "$INSTDIR"
SectionEnd

Function .onInit

  ReadRegStr $R0 HKLM \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\Linuxtrack" \
  "UninstallString"
  StrCmp $R0 "" done

  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
  "Linuxtrack-Wine is already installed. $\n$\nClick `OK` to remove the \
  previous version or `Cancel` to cancel this upgrade." \
  IDOK uninst
  Abort

;Run the uninstaller
uninst:
  ClearErrors
  Exec '$R0 /S'
done:

FunctionEnd
